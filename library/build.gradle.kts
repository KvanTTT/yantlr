import com.vanniktech.maven.publish.SonatypeHost
import org.gradle.kotlin.dsl.kotlin
import org.jetbrains.kotlin.gradle.ExperimentalKotlinGradlePluginApi
import org.jetbrains.kotlin.gradle.dsl.JvmTarget

plugins {
    alias(libs.plugins.kotlinMultiplatform)
    alias(libs.plugins.androidLibrary)
    alias(libs.plugins.vanniktech.mavenPublish)
}

group = "org.yantlr"
version = "1.0.0"

kotlin {
    jvm {
        compilerOptions {
            jvmTarget.set(JvmTarget.JVM_11)
        }
    }
    androidTarget {
        publishLibraryVariants("release")
        @OptIn(ExperimentalKotlinGradlePluginApi::class)
        compilerOptions {
            jvmTarget.set(JvmTarget.JVM_11)
        }
    }
    iosX64()
    iosArm64()
    iosSimulatorArm64()
    linuxX64()

    sourceSets {
        val commonMain by getting {
            dependencies {
                //put your multiplatform dependencies here
            }
        }
        val commonTest by getting {
            dependencies {
                implementation(libs.kotlin.test)
            }
        }
        val jvmTest by getting {
            jvmToolchain(11)
            dependencies {
                implementation(kotlin("reflect"))
                implementation(libs.kotlin.test)
            }
        }
    }

    sourceSets.forEach { _ ->
        compilerOptions {
            freeCompilerArgs.add("-Xexpect-actual-classes")
        }
    }
}

tasks.register<JavaExec>("testsGeneration") {
    println("Generate tests based on test data...")
    classpath = rootProject.project("test-generator").sourceSets["main"].runtimeClasspath
    mainClass.set("MainKt")
    args = listOf(rootProject.projectDir.toString())
}

tasks.withType<Test> {
    useJUnitPlatform()
}

tasks.named("compileTestKotlinJvm") {
    dependsOn("testsGeneration")
}

android {
    namespace = "org.jetbrains.kotlinx.multiplatform.yantlr"
    compileSdk = libs.versions.android.compileSdk.get().toInt()
    defaultConfig {
        minSdk = libs.versions.android.minSdk.get().toInt()
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }
}

mavenPublishing {
    publishToMavenCentral(SonatypeHost.CENTRAL_PORTAL)

    signAllPublications()

    coordinates(group.toString(), "yantlr", version.toString())

    pom {
        name = "yantlr"
        description = "Yet ANother Tool for Language Recognition"
        inceptionYear = "2025"
        url = "https://github.com/KvanTTT/yantlr"
        licenses {
            license {
                name = "XXX"
                url = "YYY"
                distribution = "ZZZ"
            }
        }
        developers {
            developer {
                id = "XXX"
                name = "YYY"
                url = "ZZZ"
            }
        }
        scm {
            url = "XXX"
            connection = "YYY"
            developerConnection = "ZZZ"
        }
    }
}


